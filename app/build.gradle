apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'com.neenbedankt.android-apt'

android {

   packagingOptions {
      exclude 'LICENSE.txt'
   }

   compileSdkVersion 23
   buildToolsVersion "23.0.2"

   defaultConfig {
      applicationId "packagename.app.com.appname"
      minSdkVersion 16
      targetSdkVersion 23
      versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
      String versionNameString = System.getenv("BUILD_NUMBER") ?: "IDE"
      versionName versionNameString
      if ("IDE".equals(versionNameString)) {
         buildConfigField "boolean", "IS_IDE_BUILD", "true"
      } else {
         buildConfigField "boolean", "IS_IDE_BUILD", "false"
      }
   }
   lintOptions {
      abortOnError false
      disable 'ContentDescription'
      disable 'InvalidPackage'
   }

   buildTypes {
      release {
         minifyEnabled false
         proguardFiles 'proguard-rules.pro'
      }
      debug {
         applicationIdSuffix ".debug"
         testCoverageEnabled true
      }
   }
}

task checkstyle(type: Checkstyle) {
   source 'src/'
   include '**/*.java'
   exclude '**/gen/**'
   classpath = files()
   reports {
      xml {
         destination "build/outputs/reports/checkstyle-results.xml"
      }
   }
}

task checkstyleReport(dependsOn: 'checkstyle') << {
   if (file("build/outputs/reports/checkstyle-results.xml").exists()) {
      ant.xslt(in: "build/outputs/reports/checkstyle-results.xml",
            style: "config/checkstyle/checkstyle.xsl",
            out: "build/outputs/reports/checkstyle-results.html"
      )
   }
}

dependencies {
   compile 'com.google.dagger:dagger:2.0.2'
   apt "com.google.dagger:dagger-compiler:2.0.2"
   // Needed for @Generated annotation (missing in Java <= 1.6; therefore, Android)
   compile 'javax.annotation:jsr250-api:1.0'
   compile 'com.android.support:appcompat-v7:23.1.1'
   compile 'com.android.support:design:23.1.1'
   compile 'net.hockeyapp.android:HockeySDK:3.6.2'
   compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
   compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
   compile 'com.squareup.okhttp:logging-interceptor:2.7.2'
   compile 'de.greenrobot:eventbus:2.4.0'
   compile 'com.squareup.picasso:picasso:2.5.2'
   compile 'com.jakewharton:butterknife:7.0.1'
   compile 'io.reactivex:rxandroid:1.1.0'
   compile 'io.reactivex:rxjava:1.1.0'
   androidTestCompile 'org.mockito:mockito-core:2.0.36-beta'
   androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
   androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
   androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
   androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}
